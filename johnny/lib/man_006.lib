// Persistence of Vision RayTracer version 3.0 Scene description file
// File: MAN_006.LIB
// Description: this file contains all the parts for man_006, needs BASIC.LIB too!
// Finished: 96/06/29 - 20:46
// Author: Anton Raves
//
// Version: 0004
            
            
            
#declare body =
difference {
  merge {
    box { <-7.5, 0, -3.9> <7.5, 12.4, 3.9> }
    cylinder { 12*y, 18.4*y, 2.25 }
  }
  cylinder { 0*y, 17*y, 1.25 }
  box { <-10, -2, -5> <0, 20, 5> rotate -10*z translate <-7.5, 1, 0> }
  box { <0, -2, -5> <10, 20, 5> rotate 10*z translate <7.5, 1, 0> }
  cylinder { -8*x, -3*x, 2 rotate -10*z translate <0, 8, 0> }
  cylinder { 3*x, 8*x, 2 rotate 10*z translate <0, 8, 0> }
  box { <-1.5, -1, -2.5> <1.5, 12, 2.5> }
  difference {
    box { <-6, -1, -2.5> <-1.500001, 12, 2.5> }
    box { <-10, -2, -5> <0, 20, 5> rotate -10*z translate <-6.75, 2, 0> }
    box { <0, -2, -50> <3, 20, 5> rotate 45*y translate <-1.5, 0, -1.5> }
    box { <0, -2, -50> <3, 20, 5> rotate -45*y translate <-1.5, 0, 1.5> }
  }
  difference {
    box { <1.500001, -1, -2.5> <6, 12, 2.5> }
    box { <0, -2, -5> <10, 20, 5> rotate 10*z translate <6.75, 2, 0> }
    box { <-3, -2, -50> <0, 20, 5> rotate -45*y translate <1.5, 0, -1.5> }
    box { <-3, -2, -50> <0, 20, 5> rotate 45*y translate <1.5, 0, 1.5> }
  }
  bounded_by { box { <-7.75, -0.5, -4> <7.75, 19, 4> } }
}

// This object can be used to create different clothes for the little people,
// just merge it with a body part after setting the texture for the paint
// seperately.
#declare body_paint =
difference {
  box { <-7.499999, 0.000001, -3.900001> <7.499999, 12.399999, -3.85> }
  box { <-10, -2, -5> <0, 20, 5> rotate -10*z translate <-7.5, 1, 0> }
  box { <0, -2, -5> <10, 20, 5> rotate 10*z translate <7.5, 1, 0> }
}

#declare hand =
merge {
  difference {
    cylinder { -0.5*y, 4*y, 2.25 }
    difference {
      box { <-3, -1, -2.5> <3, 2, 0> }
      cylinder { -4*x, 4*x, 2.5 translate <0, 2, 0> }
    }
    cylinder { -1*y, 5*y, 1.5 }
    box { <-0.9, -1, -3> <0.9, 5, 0> }
    rotate -15*x
    translate <0, -1.5, -3>
  }
  difference {
    cylinder { 1*z, -1.4*z, 1.3 }
    box { <-10, 0, -10> <10, 10, 10> rotate -15*x translate <0, 1, 0> }
  }
  bounded_by { box { <-3, -2.5, -6.5> <3, 3.5, 1.5> } }
}

#declare head =
difference {
  merge {
    object { button translate <0, 6, 0> }
    torus { 3, 2 translate <0, 7, 0> }
    cylinder { 3*y, 7*y, 5 }
    cylinder { 2*y, 8*y, 4 }
    torus { 3, 2 translate <0, 3, 0> }
    cylinder { 0*y, 1*y, 3.25 }
  }
  cylinder { 7*y, 10*y, 1.25 }
  cylinder { -1*y, 8*y, 2.25 }
  bounded_by { cylinder { -0.25*y, 10.5*y, 5.25 } }
}

#declare helmet =
merge {
  difference {
    merge {
      difference {
        sphere { <0, 0, 0> 6 scale <1.04, 1, 1.04> }
        box { <-7, -7, -7> <0, 7, 7> }
        box { <-7, -7, -7> <7, -0.000001, 7> }
      }
      difference {
        sphere { <0, 0, 0> 6.000001 scale <1.16, 1, 1.04> }
        box { <0.000001, -7, -7> <8, 7, 7> }
        box { <-8, -7, -7> <8, -0.000001, 7> }
      }
      difference {
        sphere { <0, 0, 0> 6.000002 scale <1.04, 2.25, 1.04> }
        box { <-7, -21, -7> <0, 1, 7> }
        box { <-7, 0.000001, -7> <7, 21, 7> }
      }
      difference {
        sphere { <0, 0, 0> 6.000003 scale <1.16, 2.25, 1.04> }
        box { <0.000001, -21, -7> <8, 1, 7> }
        box { <-8, 0.000001, -7> <8, 21, 7> }
      }
      bounded_by { box { <-7.5, -8, -6.25> <6.25, 7, 6.25> } }
      rotate -10*z
    }
    sphere { <-1, -0.25, -5.7> 0.7 }
    sphere { <-1, -0.25, 5.7> 0.7 }
    cylinder { -7*z, 7*z, 2.25 translate <-3.75, -1.2, 0 > }
    difference {
      box { <-10, -7, -8> <-4, 1.05, 8> }
      box { <-11, -6, -9> <0, 0, 9> rotate 19*z translate <-4, -3.45, 0> }
    }
    box { <-10, -10, -10> <10, 0, 10> rotate 10*z translate <0, -5, 0> }
    cylinder { -1*y, 5*y, 2.25 }
    cylinder { -9*y, 2*y, 5.1 }
    cone { 1.999999*y, 5, 4*y, 3 }
    }
  object { ring translate <0, 3, 0> }
}

#declare left_arm =
merge {
  cylinder { -1.25*x, 0*x, 1.75 }
  difference {
    sphere { <-1.25, 0, 0> 1.750001 }
    box { <-1.249999, -10, -10> <10, 10, 10> }
    box { <-10, -10, -10> <-3, 10, 10> }
    cylinder { -4*x, -1*x, 1 }
  }
  difference {
    merge {
      sphere { <0, 0, 0> 1 scale <2, 2.5, 2.5> translate <1, 0, 0> }
      cylinder { <0.5, 0, 0>, <1, -5, 0>, 1 scale <2.000001, 1, 2.5> }
      sphere { <0, 0, 0> 1 scale <2, 2.5, 2.5> translate <2, -5, 0> }
      cylinder { <2, -8, -3.5>, <2, -5, 0>, 2.15 }
    }
    box { <-10, -30, -20> <-0.300001, 10, 20> }
    cylinder { <2, -11, -7>, <2, -5, 0>, 1.35 }
  }
  bounded_by { box { <-3.5, -10.5, -5.5> <4.5, 3.5, 3.5> } }
}

#declare left_leg =
difference {
  merge {
    cylinder { -7.800001*x, -0.9*x, 3.5 }
    box { <-7.8, -11, -2> <-0.5, -0.25, 3.5> }
    box { <-7.800001, -11.000001, -4.3> <-0.499999, -8.1, 3.500001> }
  }
  cylinder { -1*x, 1*x, 3.6 }
  cylinder { 1.5*z, 4*z, 2.25 translate <-4, 1, 0> }
  cylinder { 1.5*z, 4*z, 2.25 translate <-4, -7, 0> }
  box { <-6, -12, -2.8> <-2, -9, 2.000002> }
  box { <-6, -12, -0.5> <-2, -2, 2.000001> }
  cylinder { -4*x, 0*x, 1.25 }
  box { <-5.5, -1.25, -1.5> <-3, 2.25, 2> }
  box { <-10, -20, -10> <0, 20, 10> rotate -3*z translate <-7.8, -11, 0> }
  bounded_by { box { <-8, -11.25, -4.5> <0, 3.6, 3.75> } }
}

#declare leg_joiner =
merge {
  difference {
    box { <-7, 3, -3.9> <7, 4.9, 3.9> }
    cylinder { -8*x, 8*x, 3.7 }
  }
  box { <-0.800001, 1, 0> <0.800001, 4, 0> }
  cylinder { -0.8*x, 0.8*x, 3.5 }
  cylinder { -4*x, 4*x, 1 }
  cylinder { -5*x, -4*x, 1 translate <0, -0.25, 0> }
  cylinder { 4*x, 5*x, 1 translate <0, -0.25, 0> }
  difference {
    box { <-6.5, 4.5, -2.5> <-1.5, 9.9, 2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <-6.5, 0, -2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <-6.5, 0, 2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <-1.5, 0, -2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <-1.5, 0, 2.5> }
    cylinder { 4.9*y, 10*y, 1.25 translate <-4, 0, 0> }
    box { <-2, 0, -3> <0, 4, 3> rotate -10*z translate <-6.5, 7, 0> }
  }
  difference {
    box { <1.5, 4.5, -2.5> <6.5, 9.9, 2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <6.5, 0, -2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <6.5, 0, 2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <1.5, 0, -2.5> }
    cylinder { 4.9*y, 10*y, 1 translate <1.5, 0, 2.5> }
    cylinder { 4.9*y, 10*y, 1.25 translate <4, 0, 0> }
    box { <0, 0, -3> <2, 4, 3> rotate 10*z translate <6.5, 7, 0> }
  }
  bounded_by { box { <-7.25, -3.75, -4> <7.25, 10, 4> } }
}

#declare right_arm =
merge {
  cylinder { 0*x, 1.25*x, 1.75 }
  difference {
    sphere { <1.25, 0, 0> 1.750001 }
    box { <-10, -10, -10> <1.249999, 10, 10> }
    box { <3, -10, -10> <10, 10, 10> }
    cylinder { 1*x, 4*x, 1 }
  }
  difference {
    merge {
      sphere { <0, 0, 0> 1 scale <2, 2.5, 2.5> translate <-1, 0, 0> }
      cylinder { <-0.5, 0, 0>, <-1, -5, 0>, 1 scale <2.000001, 1, 2.5> }
      sphere { <0, 0, 0> 1 scale <2, 2.5, 2.5> translate <-2, -5, 0> }
      cylinder { <-2, -8, -3.5>, <-2, -5, 0>, 2.15 }
    }
    box { <0.300001, -30, -20> <10, 10, 20> }
    cylinder { <-2, -11, -7>, <-2, -5, 0>, 1.35 }
  }
  bounded_by { box { <-4.5, -10.5, -5.5> <3.5, 3.5, 3.5> } }
}

#declare right_leg =
difference {
  merge {
    cylinder { 0.9*x, 7.800001*x, 3.5 }
    box { <0.5, -11, -2> <7.8, -0.25, 3.5> }
    box { <0.499999, -11.000001, -4.3> <7.800001, -8.1, 3.500001> }
  }
  cylinder { -1*x, 1*x, 3.6 }
  cylinder { 1.5*z, 4*z, 2.25 translate <4, 1, 0> }
  cylinder { 1.5*z, 4*z, 2.25 translate <4, -7, 0> }
  box { <2, -12, -2.8> <6, -9, 2.000002> }
  box { <2, -12, -0.5> <6, -2, 2.000001> }
  cylinder { 0*x, 4*x, 1.25 }
  box { <3, -1.25, -1.5> <5.5, 2.75, 2> }
  box { <0, -20, -10> <10, 20, 10> rotate 3*z translate <7.8, -11, 0> }
  bounded_by { box { <0, -11.25, -4.5> <8, 3.6, 3.75> } }
}

#declare vizor_1 =
merge {
  difference {
    merge {
      difference {
        cylinder { 2*y, -10*y, 6.5 scale <1.07, 1, 1> }
        box { <0.000001, -11, -7> <7, 3, 7> }
      }
      difference {
        cylinder { -6.499999*z, 6.499999*z, 1.999999 scale <1.07, 1, 1> }
        box { <-5, -3, -7> <0, 3, 7> }
      }
    }
    cylinder { 3*y, -11*y, 5.5 scale <1.07, 1, 1> }
    box { <-0.000001, -11, -5.500001> <7, 3, 5.500001> }
    box { <-20, -10, -20> <20, 0, 20> rotate 19*z translate <0, -2, 0> }
  }
  sphere { <0, 0, -5.7> 0.7 }
  sphere { <0, 0, 5.7> 0.7 }
  bounded_by { box { <-8, -6, -6.75> <2.25, 2.25, 6.75> } }
}

